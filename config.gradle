apply from: versions
if (isIndependent()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion _compileSdkVersion

    dexOptions {
        javaMaxHeapSize "4g"
    }
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false
    defaultConfig {
        if (isIndependent()) {
            if (this.name.equals("app")) {
                applicationId "com.base.project"
            } else if (this.name.contains("library")) {
                applicationId "me.csxiong.xport"
                println("this == ${this.name} , applicationId")
            } else {
                applicationId "me.csxiong.xport.${this.name}"
                println("this == ${this.name} , applicationId")
            }
        }
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode _versionCode//1 ~ 2147483647 17年10月30号1版本
        versionName _versionName


        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
//            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "armeabi"
//            abiFilters "armeabi-v7a", "x86"
            abiFilters "armeabi-v7a"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            debuggable false
            if (isIndependent()) {
                shrinkResources true
                minifyEnabled true
            }
            buildConfigField "String", "MAIN_HOST", "\"http://www.yourhost.com\""
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            crunchPngs false //关闭crunchPng优化, 以加快构建
            shrinkResources false
            minifyEnabled false
            testCoverageEnabled false
            buildConfigField "String", "MAIN_HOST", "\"http://www.yourhost.com\""
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ['libs']
            res.srcDirs = [
                    'src/main/res',
            ]
        }
    }
    if (this.name.startsWith("app")) {
        signingConfigs {
            debug {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    sourceSets {
        main {
            //未开启独立编译 并且是独立的module，才会使用总是使用 library/AndroidManifest.xml
            if (isModule && this.name.startsWith("module")) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //全部Module一起编译的时候剔除debug目录
                    exclude '**/debug/**'
                }
            }
        }
    }
    packagingOptions {
//        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/rxjava.properties'
//        exclude 'META-INF/NOTICE'
//        exclude 'META-INF/LICENSE'
//        exclude 'META-INF/LICENSE.txt'
//        exclude 'META-INF/NOTICE.txt'
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$supportLibraryVersion"
            }
        }
    }
    resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.2"
}

//dif module single build
def isIndependent() {
    if (this.name.startsWith("app")) {
        println("this module's name is not startsWith [module] , this.name = ${this.name}")
        return true
    }
    if (this.name.startsWith("library")) {
        println("this module's name is library , this.name = ${this.name}")
        return false
    }
    if (this.name.contains("module_example")) {
        println("this module's name is module , this.name = ${this.name}")
        return isExampleModule
    }
    if (this.name.contains("module_login")) {
        println("this module's name is module , this.name = ${this.name}")
        return isExampleModule
    }
    if (!isModule) {
        println("not independent!")
        return false
    }
    if (!this.name.startsWith("module")) {
        println("this module's name is not startsWith [module] , this.name = ${this.name}")
        return false
    }
    println("this module is independent!")
    return true
}
